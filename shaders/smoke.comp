#version 450
#extension GL_ARB_separate_shader_objects : enable

#define MAX_STEPS 60
#define MAX_STEPS_LIGHTS 6
#define ABSORPTION_COEFFICIENT 0.9
#define SCATTERING_ANISO 0.3
#define PI 3.14159265359

const vec3 SUN_POSITION = vec3(1.0, 0.0, 0.0);
const float MARCH_SIZE = 0.16;

struct Particle {
    vec4 position;
    vec3 velocity;
    vec4 color;
};

layout(binding = 0, rgba8) uniform image2D storageTexture;

layout (binding = 1) uniform ParameterUBO {
    float deltaTime;
    float totalTime;
    int frame;
} ubo;

layout(std140, binding = 2) buffer ParticleSSBO {
    Particle particles[];
};

layout(binding = 3) uniform sampler2D noiseTexture;
layout(binding = 4) uniform sampler2D blueNoiseTexture;


layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;


float sdSphere(vec3 p, float radius) {
    return length(p) - radius;
}

float BeersLaw (float dist, float absorption) {
    return exp(-dist * absorption);
}

float HenyeyGreenstein(float g, float mu) {
    float gg = g * g;
    return (1.0 / (4.0 * PI))  * ((1.0 - gg) / pow(1.0 + gg - 2.0 * g * mu, 1.5));
}

float hash1( float n )
{
    return fract( n*17.0*fract( n*0.3183099 ) );
}
float noise( in vec3 x )
{
    vec3 p = floor(x);
    vec3 w = fract(x);

    vec3 u = w*w*w*(w*(w*6.0-15.0)+10.0);

    float n = p.x + 317.0*p.y + 157.0*p.z;

    float a = hash1(n+0.0);
    float b = hash1(n+1.0);
    float c = hash1(n+317.0);
    float d = hash1(n+318.0);
    float e = hash1(n+157.0);
    float f = hash1(n+158.0);
    float g = hash1(n+474.0);
    float h = hash1(n+475.0);

    float k0 =   a;
    float k1 =   b - a;
    float k2 =   c - a;
    float k3 =   e - a;
    float k4 =   a - b - c + d;
    float k5 =   a - c - e + g;
    float k6 =   a - b - e + f;
    float k7 = - a + b + c - d + e - f - g + h;

    return -1.0+2.0*(k0 + k1*u.x + k2*u.y + k3*u.z + k4*u.x*u.y + k5*u.y*u.z + k6*u.z*u.x + k7*u.x*u.y*u.z);
}

float fbm(vec3 p) {
    vec3 q = p + ubo.totalTime * 0.5 * vec3(1.0, -0.2, -1.0);
    float g = noise(q);

    float f = 0.0;
    float scale = 0.5;
    float factor = 2.02;

    for (int i = 0; i < 6; i++) {
        f += scale * noise(q);
        q *= factor;
        factor += 0.21;
        scale *= 0.5;
    }

    return f;
}

float scene(vec3 p) {
    float distance1 = sdSphere(p - vec3(5.0, 0.0, 0.0), 1.5);
    float distance2 = sdSphere(p + vec3(0, 1.0, 0.0), 1.0);
    float distance3 = sdSphere(p - vec3(-3, -1.0, 0.0), 1.0);

    float f = fbm(p);

    return -min(min(distance1, distance2), distance3) + f;
}


//float scene(vec3 p) {
//    float distance = 0;
//    // Contribution from particles
//    for (int i = 0; i < particles.length(); ++i) {
//        vec3 particlePos = particles[i].position.xyz;
//        float particleRadius = 0.5; // Adjust as needed
//        distance = min(distance, sdSphere(p - particlePos, particleRadius));
//    }
//
//    float f = fbm(p);
//
//    return -distance + f;
//}

float lightmarch(vec3 position, vec3 rayDirection) {
    vec3 sunDirection = normalize(SUN_POSITION);
    float totalDensity = 0.0;
    float marchSize = 0.03;

    for (int step = 0; step < MAX_STEPS_LIGHTS; step++) {
        position += sunDirection * marchSize * float(step);

        float lightSample = scene(position);
        totalDensity += lightSample;
    }

    float transmittance = BeersLaw(totalDensity, ABSORPTION_COEFFICIENT);
    return transmittance;
}

float raymarch(vec3 rayOrigin, vec3 rayDirection, float offset) {
    float depth = 0.0;
    depth += MARCH_SIZE * offset;
    vec3 p = rayOrigin + depth * rayDirection;
    vec3 sunDirection = normalize(SUN_POSITION);

    float totalTransmittance = 1.0;
    float lightEnergy = 0.0;

    float phase = HenyeyGreenstein(SCATTERING_ANISO, dot(rayDirection, sunDirection));

    for (int i = 0; i < MAX_STEPS; i++) {
        float density = scene(p);

        // We only draw the density if it's greater than 0
        if (density > 0.0) {
            float lightTransmittance = lightmarch(p, rayDirection);
            float luminance = 0.025 + density * phase;

            totalTransmittance *= lightTransmittance;
            lightEnergy += totalTransmittance * luminance;
        }

        depth += MARCH_SIZE;
        p = rayOrigin + depth * rayDirection;
    }

    return clamp(lightEnergy, 0.0, 1.0);
}

void main() {
    vec2 screenSize = imageSize(storageTexture);
    float horizontalCoefficient = (2.0 * float(gl_GlobalInvocationID.x) / screenSize.x) - 1.0;
    float verticalCoefficient = (2.0 * float(gl_GlobalInvocationID.y) / screenSize.y) - 1.0;

    // hard coded camera position
    vec3 ro = vec3(0, 0, 7.0);
    vec3 rd = normalize(vec3(horizontalCoefficient, verticalCoefficient, -1.0));

    // Sun and Sky
    vec3 sunColor = vec3(1.0,0.5,0.3);
    vec3 sunDirection = normalize(SUN_POSITION);
    float sun = clamp(dot(sunDirection, rd), 0.0, 1.0 );
    // Base sky color
    vec3 color = vec3(0.7,0.7,0.90);
    // Add vertical gradient
    color -= 0.8 * vec3(0.90,0.75,0.90) * rd.y;
    // Add sun color to sky
    color += 0.5 * vec3(1.0,0.5,0.3) * pow(sun, 10.0);

    float blueNoise = texture(blueNoiseTexture, gl_GlobalInvocationID.xy / 1024).r;
    float offset = fract(blueNoise + float(ubo.frame%32) / sqrt(0.5));

    // Cloud
    float res = raymarch(ro, rd, offset);
    color = color + sunColor * res;

    imageStore(storageTexture, ivec2(gl_GlobalInvocationID.xy), vec4(color, 1.0));
}
